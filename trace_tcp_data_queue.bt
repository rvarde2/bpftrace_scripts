#!/usr/bin/env bpftrace

#include <net/tcp.h>
#include <net/sock.h>

// run: ./trace_tcp_data_queue.bt 5201

kprobe:tcp_data_queue
{
    $sk  = (struct sock *)arg0;
    $skb = (struct sk_buff *)arg1;

    //Ignore packets without payload: Ignore Acks: Makes it receiver side script
    if ($skb->len == 0) {
        return;
    }

    $lport = $sk->__sk_common.skc_num; //Listening Port
    $sport_be = $sk->__sk_common.skc_dport; //Sender Port
    $sport = (($sport_be & 0xFF00) >> 8) | (($sport_be & 0x00FF) << 8);

    //if ($sport == $1 && $lport == $2)
    if ($lport == $1){
        $tp = (struct tcp_sock *)$sk;
        $rcv_nxt = $tp->rcv_nxt;
        $tcb = (struct tcp_skb_cb *)$skb->cb;
        $seq = $tcb->seq;

        if ($seq > $rcv_nxt) {
            $gap = ($seq - $rcv_nxt)/8800;
            @gap_max  = max($gap);
            @gap_sum += $gap;
            @gap_cnt += 1;
            @reorder_total = count();

            $iph = (struct iphdr *)($skb->head + $skb->network_header);

            /*
            $ecn = $iph->tos & 0x3;
            if ($ecn == 0x3) {
                @reorder_ce = count();
            }
            */
            
            $compress = $iph->tos & 0x4;
            if ($compress == 0x4) {
                @gap_compress_hist = hist($gap);
            }
            else{
                @gap_encrypt_hist = hist($gap);
            }

        }
        @total_packets = count();
        @reordering_thresh = hist($tp->reordering);
    }
}

interval:s:1
{
    printf("\n===== SUMMARY =====\n");

    print(@total_packets);
    print(@gap_cnt);

    if (@gap_cnt > 0) {
        print(@gap_max);
        printf("average_gap:%d",@gap_sum / @gap_cnt);
        print(@gap_compress_hist);
        print(@gap_encrypt_hist);
        print(@reordering_thresh);
    }

    printf("=====================\n");

    // Reset for next window
    clear(@gap_compress_hist);
    clear(@gap_encrypt_hist);
    clear(@gap_max);
    clear(@gap_sum);
    clear(@gap_cnt);
    clear(@reordering_thresh);
    clear(@total_packets);
}
